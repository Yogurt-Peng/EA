#include "Tools.mqh"
input group "åŸºæœ¬å‚æ•°";
input int MagicNumber = 7456;                     // EAç¼–å·
input ENUM_TIMEFRAMES TimeFrame = PERIOD_CURRENT; // å‘¨æœŸ
input double LotSize = 0.01;                      // æ‰‹æ•°
input int StopLoss = 100;                         // æ­¢æŸç‚¹æ•° 0:ä¸ä½¿ç”¨
input int TakeProfit = 100;                       // æ­¢ç›ˆç‚¹æ•° 0:ä¸ä½¿ç”¨

input group "æŒ‡æ ‡å‚æ•°";
input bool UseFractal = true; // åˆ†å‹
input bool UseJaws1 = true;   // é³„é±¼çº¿1
input bool UseJaws2 = true;   // é³„é±¼çº¿2
input bool UseAC = true;      // åŠ é€Ÿ

//+------------------------------------------------------------------+

CTrade trade;
COrderInfo orderInfo;
CPositionInfo positionInfo;
CTools tools(_Symbol, &trade, &positionInfo, &orderInfo);

int handleFractals;  // åˆ†å‹
int handleAlligator; // é³„é±¼çº¿
int handleAO;        // åŠ¨é‡
int handleAC;        // åŠ é€Ÿ

double bufferFractalsUp[];
double bufferFractalsDown[];
double bufferAlligatorJaws[];
double bufferAlligatorTeeth[];
double bufferAlligatorLips[];
double bufferAO[];
double bufferAC[];

//+------------------------------------------------------------------+
int OnInit()
{
    handleFractals = iFractals(_Symbol, TimeFrame);
    if (handleFractals == INVALID_HANDLE)
    {
        Alert("iFractals æŒ‡æ ‡åŠ è½½å¤±è´¥ï¼Œè¯·æ£€æŸ¥ç›¸å…³è®¾å®šæ˜¯å¦åˆç†ï¼Œæˆ–è€…æ£€æŸ¥æ‚¨çš„mt5æ˜¯å¦åŒ…å«ç›¸å…³æŒ‡æ ‡");
        return (INIT_FAILED);
    }

    handleAlligator = iAlligator(_Symbol, TimeFrame, 13, 8, 8, 5, 5, 3, MODE_SMMA, PRICE_MEDIAN);
    if (handleAlligator == INVALID_HANDLE)
    {
        Alert("handleAlligator æŒ‡æ ‡åŠ è½½å¤±è´¥ï¼Œè¯·æ£€æŸ¥ç›¸å…³å‚æ•°è®¾å®šæ˜¯å¦åˆç†ï¼Œæˆ–è€…æ£€æŸ¥æ‚¨çš„mt5æ˜¯å¦æœ‰å¯¹åº”çš„æŒ‡æ ‡å­˜åœ¨");
        return (INIT_FAILED);
    }

    handleAO = iAO(_Symbol, TimeFrame);
    if (handleAO == INVALID_HANDLE)
    {
        Alert("iAO æŒ‡æ ‡åŠ è½½å¤±è´¥ï¼Œè¯·æ£€æŸ¥ç›¸å…³å‚æ•°è®¾å®šæ˜¯å¦åˆç†ï¼Œæˆ–è€…æ£€æŸ¥æ‚¨çš„mt5æ˜¯å¦æœ‰å¯¹åº”çš„æŒ‡æ ‡å­˜åœ¨");
        return (INIT_FAILED);
    }

    handleAC = iAC(_Symbol, TimeFrame);
    if (handleAC == INVALID_HANDLE)
    {
        Alert("iAC æŒ‡æ ‡åŠ è½½å¤±è´¥ï¼Œè¯·æ£€æŸ¥ç›¸å…³å‚æ•°è®¾å®šæ˜¯å¦åˆç†ï¼Œæˆ–è€…æ£€æŸ¥æ‚¨çš„mt5æ˜¯å¦æœ‰å¯¹åº”çš„æŒ‡æ ‡å­˜åœ¨");
        return (INIT_FAILED);
    }

    ChartIndicatorAdd(0, 0, handleFractals);
    ChartIndicatorAdd(0, 0, handleAlligator);
    ChartIndicatorAdd(0, 1, handleAC);
    ChartIndicatorAdd(0, 2, handleAO);

    ArraySetAsSeries(bufferFractalsUp, true);
    ArraySetAsSeries(bufferFractalsDown, true);
    ArraySetAsSeries(bufferAlligatorJaws, true);
    ArraySetAsSeries(bufferAlligatorTeeth, true);
    ArraySetAsSeries(bufferAlligatorLips, true);
    ArraySetAsSeries(bufferAO, true);
    ArraySetAsSeries(bufferAC, true);

    trade.SetExpertMagicNumber(MagicNumber);
    Print("ğŸš€ğŸš€ğŸš€ åˆå§‹åŒ–æˆåŠŸ");
    return INIT_SUCCEEDED;
}

void OnTick()
{

    if (CopyBuffer(handleFractals, 0, 3, 1, bufferFractalsUp) < 1)
    {
        Print("CopyBuffer bufferFractalsUp é”™è¯¯");
        return;
    }

    if (CopyBuffer(handleFractals, 1, 3, 1, bufferFractalsDown) < 1)
    {
        Print("CopyBuffer bufferFractalsDown é”™è¯¯");
        return;
    }

    if (CopyBuffer(handleAlligator, 0, 0, 3, bufferAlligatorJaws) < 3)
    {
        Print("CopyBuffer bufferAlligatorJaws é”™è¯¯");
        return;
    }

    if (CopyBuffer(handleAlligator, 1, 0, 3, bufferAlligatorTeeth) < 3)
    {
        Print("CopyBuffer bufferAlligatorTeeth é”™è¯¯");
        return;
    }

    if (CopyBuffer(handleAlligator, 2, 0, 3, bufferAlligatorLips) < 3)
    {
        Print("CopyBuffer bufferAlligatorLips é”™è¯¯");
        return;
    }

    if (CopyBuffer(handleAO, 0, 0, 3, bufferAO) < 3)
    {
        Print("CopyBuffer bufferAO é”™è¯¯");
        return;
    }
    // è·å–é¢œè‰² 1:ç»¿ 0:çº¢
    if (CopyBuffer(handleAC, 1, 0, 3, bufferAC) < 3)
    {
        Print("CopyBuffer bufferAC é”™è¯¯");
        return;
    }

    if (tools.IsNewBar(TimeFrame))
    {

        // åˆ†å‹
        bool sigSell = false;
        bool sigBuy = false;
        if (UseFractal)
        {
            if (bufferFractalsUp[0] != EMPTY_VALUE)
                sigSell = true;
            if (bufferFractalsDown[0] != EMPTY_VALUE)
                sigBuy = true;
        }
        else
        {
            sigSell = false;
            sigBuy = false;
        }
        // é³„é±¼çº¿
        bool sigJawBuy1 = false;
        bool sigJawSell1 = false;
        if (UseJaws1)
        {
            if (iClose(_Symbol, TimeFrame, 1) > bufferAlligatorJaws[1])
                sigJawBuy1 = true;
            if (iClose(_Symbol, TimeFrame, 1) < bufferAlligatorJaws[1])
                sigJawSell1 = true;
        }
        else
        {
            sigJawBuy1 = true;
            sigJawSell1 = true;
        }

        bool sigJawBuy2 = false;
        bool sigJawSell2 = false;
        if (UseJaws2)
        {   
            if (iClose(_Symbol, TimeFrame, 2) < bufferAlligatorJaws[2] && iClose(_Symbol, TimeFrame, 1) > bufferAlligatorJaws[1])
                sigJawBuy2 = true;
            if (iClose(_Symbol, TimeFrame, 2) > bufferAlligatorJaws[2] && iClose(_Symbol, TimeFrame, 1) < bufferAlligatorJaws[1])
                sigJawSell2 = true;
        }
        else
        {
            sigJawBuy2 = true;
            sigJawSell2 = true;
        }

        // AO
        bool sigAOBuy = bufferAO[1] > 0 && bufferAO[2] < 0;
        bool sigAOSell = bufferAO[1] < 0 && bufferAO[2] > 0;

        // AC
        bool sigAcBuy = false;
        bool sigAcSell = false;
        if (UseAC)
        {
            if (bufferAC[1] == 0)
                sigAcBuy = true;
            if (bufferAC[1] == 1)
                sigAcSell = true;
        }
        else
        {
            sigAcBuy = true;
            sigAcSell = true;
        }

        if (sigBuy && sigJawBuy1 && sigJawBuy2 && sigAcBuy && sigAOBuy)
        {
            trade.Buy(LotSize);
        }

        if (sigSell && sigJawSell1 && sigJawSell2 && sigAcSell && sigAOSell)
        {
            trade.Sell(LotSize);
        }

        if (sigAOSell)
        {
            Print("close all buy");
            tools.CloseAllPositions(MagicNumber, POSITION_TYPE_BUY);
        }

        if (sigAOBuy)
        {
            Print("close all sell");
            tools.CloseAllPositions(MagicNumber, POSITION_TYPE_SELL);

        }
    }
}

void OnDeinit(const int reason)
{
    Print("ğŸš€ğŸš€ğŸš€ EAç§»é™¤");
}
