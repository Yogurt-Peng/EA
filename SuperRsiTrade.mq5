#include "Tools.mqh"
input group "åŸºæœ¬å‚æ•°";
input int MagicNumber = 56712;                    // EAç¼–å·
input ENUM_TIMEFRAMES TimeFrame = PERIOD_CURRENT; // å‘¨æœŸ
input double LotSize = 0.01;                      // æ‰‹æ•°
input int StopLoss = 0;                         // æ­¢æŸç‚¹æ•° 0:ä¸ä½¿ç”¨
input int TakeProfit = 0;                       // æ­¢ç›ˆç‚¹æ•° 0:ä¸ä½¿ç”¨

input group "æŒ‡æ ‡å‚æ•°";
input int RSIValue = 14;      // RSIæŒ‡æ ‡å€¼
input int RSIOverbought = 70; // è¶…ä¹°åŒº
input int RSIOversold = 30;   // è¶…å–åŒº
input int BBValue = 20;       // Bollinger BandsæŒ‡æ ‡å€¼
input int BBDeviation = 2;    // Bollinger BandsæŒ‡æ ‡å€¼

input group "è¿‡æ»¤å‚æ•°";
input bool MAFilter = true;                     // æ˜¯å¦ä½¿ç”¨MAè¿‡æ»¤
input bool IsReverse = true;                   // æ˜¯å¦åå‘è¿‡æ»¤æ¡ä»¶
input ENUM_TIMEFRAMES MAFilterTF = PERIOD_M15;   // è¿‡æ»¤MAå¸¦å‘¨æœŸ
input int MAFilterValue = 80;                   // MAæŒ‡æ ‡å€¼
input ENUM_MA_METHOD MAFilterMethod = MODE_SMA; // è¿‡æ»¤MAæŒ‡æ ‡ç±»åž‹

//+------------------------------------------------------------------+


CTrade trade;
CTools tools(_Symbol, &trade);


int handleRSI;
int handleBB;
int handleMA;

double bufferRSIValue[];
double bufferBBValue[];
double bufferMAValue[];

//+------------------------------------------------------------------+

int OnInit()
{
    Print("ðŸš€ðŸš€ðŸš€ SuperRSITradeåˆå§‹åŒ–ä¸­...");

    handleRSI = iRSI(_Symbol, TimeFrame, RSIValue, PRICE_CLOSE);
    handleBB = iBands(_Symbol, TimeFrame, BBValue, 0, BBDeviation, PRICE_CLOSE);
    handleMA = iMA(_Symbol, MAFilterTF, MAFilterValue, 0, MAFilterMethod, PRICE_CLOSE);
    ChartIndicatorAdd(0,1,handleRSI);
    ChartIndicatorAdd(0,0,handleBB);
    ChartIndicatorAdd(0,0,handleMA);

    if (handleRSI == INVALID_HANDLE || handleBB == INVALID_HANDLE || handleMA == INVALID_HANDLE)
    {
        Print("ðŸš€ðŸš€ðŸš€ SuperRSITradeæŒ‡æ ‡åˆå§‹åŒ–å¤±è´¥");
        return INIT_FAILED;
    }

    ArraySetAsSeries(bufferRSIValue, true);
    ArraySetAsSeries(bufferBBValue, true);
    ArraySetAsSeries(bufferMAValue, true);
    trade.SetExpertMagicNumber(MagicNumber);
    Print("ðŸš€ðŸš€ðŸš€ SuperRSITradeåˆå§‹åŒ–æˆåŠŸ");
    return INIT_SUCCEEDED;
}

void OnTick()
{
    double ask = SymbolInfoDouble(_Symbol, SYMBOL_ASK);
    double bid = SymbolInfoDouble(_Symbol, SYMBOL_BID);
    double yesterdayClose = iClose(_Symbol, MAFilterTF, 1);

    double buySl = (StopLoss == 0) ? 0 : ask - StopLoss * _Point;
    double buyTp = (TakeProfit == 0) ? 0 : ask + TakeProfit * _Point;
    double sellSl = (StopLoss == 0) ? 0 : bid + StopLoss * _Point;
    double sellTp = (TakeProfit == 0) ? 0 : bid - TakeProfit * _Point;




    if (tools.GetPositionCount(MagicNumber) > 0)
    {
        CopyBuffer(handleBB, 1, 0, 1, bufferBBValue);
        if (SymbolInfoDouble(_Symbol, SYMBOL_ASK) >= bufferBBValue[0])
        {
            tools.CloseAllPositions(MagicNumber, POSITION_TYPE_BUY);
        }
        CopyBuffer(handleBB, 2, 0, 1, bufferBBValue);
        if (SymbolInfoDouble(_Symbol, SYMBOL_BID) <= bufferBBValue[0])
        {
            tools.CloseAllPositions(MagicNumber, POSITION_TYPE_SELL);
        }
        return;
    }

    if (!tools.IsNewBar(TimeFrame))
        return;

    bool buyCondition = false;
    bool sellCondition = false;

    if (MAFilter)
    {
        CopyBuffer(handleMA, 0, 1, 1, bufferMAValue);
        if (IsReverse)
        {
            buyCondition = yesterdayClose < bufferMAValue[0] && GetSign() == BUY;
            sellCondition = yesterdayClose > bufferMAValue[0] && GetSign() == SELL;
        }
        else
        {
            buyCondition = yesterdayClose > bufferMAValue[0] && GetSign() == BUY;
            sellCondition = yesterdayClose < bufferMAValue[0] && GetSign() == SELL;
        }
    }
    else
    {
        buyCondition = GetSign() == BUY;
        sellCondition = GetSign() == SELL;
    }

    if (buyCondition)
    {
        trade.Buy(LotSize, _Symbol, ask, buySl, buyTp, "SuperRSITrend");
    }
    else if (sellCondition)
    {
        trade.Sell(LotSize, _Symbol, bid, sellSl, sellTp, "SuperRSITrend");
    }
}

void OnDeinit(const int reason)
{
    IndicatorRelease(handleRSI);
    IndicatorRelease(handleBB);
    IndicatorRelease(handleMA);
    Print("ðŸš€ðŸš€ðŸš€ SuperRSITradeç§»é™¤");
}

SIGN GetSign()
{
    CopyBuffer(handleRSI, 0, 1, 2, bufferRSIValue);

    if (bufferRSIValue[1] > RSIOverbought && bufferRSIValue[0] < RSIOverbought)
        return SELL;

    else if (bufferRSIValue[1] < RSIOversold && bufferRSIValue[0] > RSIOversold)
        return BUY;

    return NONE;
}
