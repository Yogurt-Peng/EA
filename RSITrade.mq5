#include "Tools.mqh"
#include "Indicators.mqh"
input group "åŸºæœ¬å‚æ•°";
input int MagicNumber = 52422;                    // EAç¼–å·
input ENUM_TIMEFRAMES TimeFrame = PERIOD_CURRENT; // å‘¨æœŸ
input double LotSize = 0.01;                      // æ‰‹æ•°
input int StopLoss = 0;                           // æ­¢æŸç‚¹æ•° 0:ä¸ä½¿ç”¨
input int TakeProfit = 0;                         // æ­¢ç›ˆç‚¹æ•° 0:ä¸ä½¿ç”¨

input group "æŒ‡æ ‡å‚æ•°";
input int RSIValue = 14;      // RSIæŒ‡æ ‡å€¼
input int RSIOverbought = 70; // è¶…ä¹°åŒº
input int RSIOversold = 30;   // è¶…å–åŒº
input int BBValue = 20;       // Bollinger BandsæŒ‡æ ‡å€¼
input int BBDeviation = 2;    // Bollinger BandsæŒ‡æ ‡å€¼

input group "è¿‡æ»¤å‚æ•°";
input bool MAFilter = true;                     // æ˜¯å¦ä½¿ç”¨MAè¿‡æ»¤
input bool IsReverse = true;                    // æ˜¯å¦åå‘è¿‡æ»¤æ¡ä»¶
input ENUM_TIMEFRAMES MAFilterTF = PERIOD_M15;  // è¿‡æ»¤MAå¸¦å‘¨æœŸ
input int MAFilterValue = 80;                   // MAæŒ‡æ ‡å€¼
input ENUM_MA_METHOD MAFilterMethod = MODE_SMA; // è¿‡æ»¤MAæŒ‡æ ‡ç±»åž‹

CTrade trade;
CTools tools(_Symbol, &trade);

CRSI rsi(_Symbol, TimeFrame, RSIValue);
CBollingerBands bollinger(_Symbol, TimeFrame, BBValue, BBDeviation);
CMA ma(_Symbol, MAFilterTF, MAFilterValue, MAFilterMethod);

datetime FilterTime = 0;

int OnInit()
{
    Print("ðŸš€ðŸš€ðŸš€ RSITradeåˆå§‹åŒ–ä¸­...");
    rsi.Initialize();
    bollinger.Initialize();
    ma.Initialize();

    ChartIndicatorAdd(0, 1, rsi.GetHandle());
    ChartIndicatorAdd(0, 0, bollinger.GetHandle());
    ChartIndicatorAdd(0, 0, ma.GetHandle());

    trade.SetExpertMagicNumber(MagicNumber);
    Print("ðŸš€ðŸš€ðŸš€ RSITradeåˆå§‹åŒ–æˆåŠŸ");
    return INIT_SUCCEEDED;
}

void OnTick()
{
    double ask = SymbolInfoDouble(_Symbol, SYMBOL_ASK);
    double bid = SymbolInfoDouble(_Symbol, SYMBOL_BID);
    double close = iClose(_Symbol, TimeFrame, 1);

    double buySl = (StopLoss == 0) ? 0 : ask - StopLoss * _Point;
    double buyTp = (TakeProfit == 0) ? 0 : ask + TakeProfit * _Point;
    double sellSl = (StopLoss == 0) ? 0 : bid + StopLoss * _Point;
    double sellTp = (TakeProfit == 0) ? 0 : bid - TakeProfit * _Point;


    if (tools.GetPositionCount(MagicNumber) > 0)
    {

        if (ask >= bollinger.GetValue(1, 0))
        {
            tools.CloseAllPositions(MagicNumber, POSITION_TYPE_BUY);
        }
        if (bid <= bollinger.GetValue(2, 0))
        {
            tools.CloseAllPositions(MagicNumber, POSITION_TYPE_SELL);
        }
        return;
    }

    if (!tools.IsNewBar(TimeFrame))
        return;






    bool buyCondition = false;
    bool sellCondition = false;

    if (MAFilter)
    {
        if (IsReverse)
        {
            buyCondition = close < ma.GetValue(1) && GetSign() == BUY;
            sellCondition = close > ma.GetValue(1) && GetSign() == SELL;
        }
        else
        {
            buyCondition = close > ma.GetValue(1) && GetSign() == BUY;
            sellCondition = close < ma.GetValue(1) && GetSign() == SELL;
        }
    }
    else
    {
        buyCondition = GetSign() == BUY;
        sellCondition = GetSign() == SELL;
    }

    if (buyCondition)
    {
        trade.Buy(LotSize, _Symbol, ask, buySl, buyTp, "RSITrend");
    }
    else if (sellCondition)
    {
        trade.Sell(LotSize, _Symbol, bid, sellSl, sellTp, "RSITrend");
    }
}

void OnTrade()
{
}

void OnDeinit(const int reason)
{

    IndicatorRelease(rsi.GetHandle());
    IndicatorRelease(bollinger.GetHandle());
    IndicatorRelease(ma.GetHandle());
    Print("ðŸš€ðŸš€ðŸš€ SuperRSITradeç§»é™¤");
}

SIGN GetSign()
{

    if (rsi.GetValue(2) > RSIOverbought && rsi.GetValue(1) < RSIOverbought)
        return SELL;

    else if (rsi.GetValue(2) < RSIOversold && rsi.GetValue(1) > RSIOversold)
        return BUY;

    return NONE;
}
